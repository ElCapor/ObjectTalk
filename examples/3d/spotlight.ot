#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2022 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class App : gui.App {
	function setup(this) {
		var ambient = gui.AmbientLight()
			.setColorRGB(0.5, 0.5, 0.5);

		var light = gui.SpotLight()
			.setDirection(0.0, -0.2, -1.0);

		var wall = gui.Mesh()
			.setGeometry(gui.PlaneGeometry(10, 10))
			.setMaterial(gui.ColoredMaterial("gold"))
			.rotateX(-pi / 4.0);

		var scene = gui.Scene()
			.add(ambient)
			.add(light)
			.add(wall);

		var camera = gui.Camera();

		var view = gui.View()
			.setScreenArea(25, 0, 75, 100)
			.setCamera(camera)
			.setScene(scene);

		var panel = gui.Panel(0, 0, 25, 100);

		var node = gui.TreeNode("Camera:");
		node.add(gui.CameraController(camera));
		panel.add(node);

		node = gui.TreeNode("Ambient light:");
		node.add(gui.AmbientLightController(ambient));
		panel.add(node);

		node = gui.TreeNode("Spot light:");
		node.add(gui.SpotLightController(light));
		panel.add(node);

		this.add(view).add(panel);
	}
}

var app = App();
os.runGUI();
