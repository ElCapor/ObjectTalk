#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2023 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class App : gui.App {
	function setup(this) {
		var ambient = gui.AmbientLight();
		var light = gui.DirectionalLight(-1, -2, -4);

		var geometry = gui.SphereGeometry();
		var gold = gui.ColoredMaterial("gold");
		var object = gui.Mesh(geometry, gold).setWireframe(true);

		this.addAnimation(gui.Animation()
			.from(0.0)
			.to(pi * 2.0)
			.during(15000)
			.continuous()
			.onStep(function(angle) {
				object.rotateAroundVector(angle, 1, 1, 0);
			}));

		var scene = gui.Scene()
			.add(ambient)
			.add(light)
			.add(object);

		var camera = gui.Camera()
			.setPerspective(45, 0.1, 100)
			.setPosition(0, 0, 4);

		var view = gui.View()
			.setScreenArea(25, 0, 75, 100)
			.setCamera(camera)
			.setScene(scene);

		var panel = gui.Panel(0, 0, 25, 100);

		var node = gui.TreeNode("Camera:");
		node.add(gui.CameraController(camera));
		panel.add(node);

		node = gui.TreeNode("Ambient Light:");
		node.add(gui.AmbientLightController(ambient));
		panel.add(node);

		node = gui.TreeNode("Directional Light:");
		node.add(gui.DirectionalLightController(light));
		panel.add(node);

		node = gui.TreeNode("Cylinder:");

		node.add(gui.RealSlider("Radius", 0, 4, 1, function(value) {
			geometry.setRadius(value);
		}));

		node.add(gui.IntegerSlider("Width Segments", 4, 64, 32, function(value) {
			geometry.setWidthSegments(value);
		}));

		node.add(gui.IntegerSlider("Height Segments", 4, 64, 16, function(value) {
			geometry.setHeightSegments(value);
		}));

		node.add(gui.RealSlider("phiStart", 0, pi * 2.0, 0, function(value) {
			geometry.setPhiStart(value);
		}));

		node.add(gui.RealSlider("phiLength", 0, pi * 2, pi * 2, function(value) {
			geometry.setPhiLength(value);
		}));

		node.add(gui.RealSlider("thetaStart", 0, pi, 0, function(value) {
			geometry.setThetaStart(value);
		}));

		node.add(gui.RealSlider("thetaLength", 0, pi, pi, function(value) {
			geometry.setThetaLength(value);
		}));

		panel.add(node);
		this.add(view).add(panel);
	}
}

var app = App();
app.run();
