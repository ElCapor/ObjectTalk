#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2022 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class App : gui.App {
	function setup(this) {
		var ambient = gui.Ambient();
		var light = gui.Light().setDirection(1, 2, 4);

		var geometry = gui.CylinderGeometry();
		var gold = gui.Material("material", "gold");
		var object = gui.Mesh(geometry, gold).setWireframe(true);

		this.addAnimation(gui.Animation()
			.from(0.0)
			.to(pi * 2.0)
			.during(15000)
			.continuous()
			.onStep(function(angle) {
				object.rotateAroundVector(angle, 1, 1, 0);
			}));

		var scene = gui.Scene()
			.add(ambient)
			.add(light)
			.add(object);

		var camera = gui.Camera()
			.setPerspective(45, 0.1, 100)
			.setPosition(0, 0, 4);

		var view = gui.View()
			.setScreenArea(25, 0, 75, 100)
			.setCamera(camera)
			.setScene(scene);

		var panel = gui.Panel(0, 0, 25, 100);

		var node = gui.TreeNode("Camera:");
		node.add(gui.CameraController(camera));
		panel.add(node);

		node = gui.TreeNode("Ambient Light:");
		node.add(gui.AmbientController(ambient));
		panel.add(node);

		node = gui.TreeNode("Point Light:");
		node.add(gui.LightController(light));
		panel.add(node);

		node = gui.TreeNode("Cylinder:");

		node.add(gui.RealSlider("Top Radius", 0, 4, 1, function(value) {
			geometry.setTopRadius(value);
		}));

		node.add(gui.RealSlider("Bottom Radius", 0, 4, 1, function(value) {
			geometry.setBottomRadius(value);
		}));

		node.add(gui.RealSlider("Height", 0.5, 4, 1, function(value) {
			geometry.setHeight(value);
		}));

		node.add(gui.IntegerSlider("Radial Segments", 4, 64, 16, function(value) {
			geometry.setRadialSegments(value);
		}));

		node.add(gui.IntegerSlider("Height Segments", 4, 64, 1, function(value) {
			geometry.setHeightSegments(value);
		}));

		node.add(gui.Checkbox("Open ended", false, function(value) {
			geometry.setOpenEnded(value);
		}));

		node.add(gui.RealSlider("thetaStart", 0, pi, 0, function(value) {
			geometry.setThetaStart(value);
		}));

		node.add(gui.RealSlider("thetaLength", 0, pi * 2, pi * 2, function(value) {
			geometry.setThetaLength(value);
		}));

		panel.add(node);
		this.add(view).add(panel);
	}
}

var app = App();
os.runGUI();
