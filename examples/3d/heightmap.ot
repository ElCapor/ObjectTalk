#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2021 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class MyApp : gui.Application {
	function setup(this, screen) {
		var ambient = gui.Ambient();
		var light = gui.Light().setPosition(20, 400, -400);

		var heightmap = gui.HeightMap()
			.setScale(40.0)
			.setOffset(-5.0)
			.loadMap(__DIR__ + "/assets/heightmap.png");

		var terrain = gui.Mesh()
			.setGeometry(gui.PlaneGeometry()
				.setWidth(200)
				.setHeight(200)
				.setWidthSegments(100)
				.setHeightSegments(100)
				.setHeightMap(heightmap))
			.setMaterial(gui.Material("material", "gold"))
			.setWireframe(true)
			.rotateX(-pi / 2.0);

		var scene = gui.Scene()
			.add(ambient)
			.add(light)
			.add(terrain);

		var camera = gui.Camera()
			.setMouseControl(true)
			.setAngle(pi / 4.0)
			.setPitchLimits(0.1, pi / 2.0 - 0.1)
			.setPitch(0.3)
			.setDistanceLimits(20.0, 1000.0)
			.setDistance(300.0)
			.setFOV(45)
			.setClipping(0.1, 1000);

		var view = gui.View()
			.setScreenArea(20, 0, 80, 100)
			.setCamera(camera)
			.setScene(scene);

		var panel = gui.Panel(0, 0, 20, 100);

		var node = gui.TreeNode("Height Map:");
		node.add(gui.HeightMapController(heightmap));
		panel.add(node);

		screen.add(view).add(panel);
	}
}

var app = MyApp();
app.run("Height Map Demo");
