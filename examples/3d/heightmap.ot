#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2021 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class MyApp : gui.Application {
	function setup(this, screen) {
		var ambient = gui.Ambient();
		var light = gui.Light().setPosition(20, 300, 40);

		var heightmap = gui.HeightMap()
			.setClamp(0.35, 1.0)
			.setOffset(-0.35)
			.setScale(300)
			.generate(256, 8, 2);

		var terrain = gui.Mesh()
			.setGeometry(gui.PlaneGeometry()
				.setWidth(256)
				.setHeight(256)
				.setWidthSegments(100)
				.setHeightSegments(100)
				.setHeightMap(heightmap))
			.setMaterial(gui.Material("material", "gold"))
			.setWireframe(true);

		var scene = gui.Scene()
			.add(ambient)
			.add(light)
			.add(terrain);

		var camera = gui.Camera()
			.setMouseControl(true)
			.setAngle(0.7)
			.setPitchLimits(0.1, pi / 2.0)
			.setPitch(1.2)
			.setDistanceLimits(20.0, 1000.0)
			.setDistance(500.0)
			.setFOV(45)
			.setClipping(0.1, 1000);

		var view = gui.View()
			.setCamera(camera)
			.setScene(scene);

		screen.add(view);
	}
}

var app = MyApp();
app.run("3D Terrain");
