#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2023 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class App : gui.App {
	function setup(this) {
		var ambient = gui.AmbientLight();
		var light = gui.DirectionalLight(-20, -400, 400);

		var noisemap = gui.NoiseMap()
			.setScaleZ(200.0)
			.setOffsetZ(-100.0);

		var terrain = gui.Mesh()
			.setGeometry(gui.PlaneGeometry()
				.setWidth(200)
				.setHeight(200)
				.setWidthSegments(100)
				.setHeightSegments(100)
				.setNoiseMap(noisemap, 0.0, 0.0))
			.setMaterial(gui.ColoredMaterial("gold"))
			.setWireframe(true)
			.rotateX(-pi / 2.0);

		var scene = gui.Scene()
			.add(ambient)
			.add(light)
			.add(terrain);

		var camera = gui.Camera()
			.setPerspective(45, 0.1, 1000)
			.setCircleTargetMode()
			.setDistanceLimits(20.0, 1000.0)
			.setDistance(300.0)
			.setPitchLimits(0.1, pi / 2.0 - 0.1)
			.setPitch(0.3)
			.setYaw(pi / 4.0);

		var view = gui.View()
			.setScreenArea(20, 0, 80, 100)
			.setCamera(camera)
			.setScene(scene);

		var panel = gui.Panel(0, 0, 20, 100);

		var node = gui.TreeNode("Noise Map:");
		node.add(gui.NoiseMapController(noisemap));
		panel.add(node);

		this.add(view).add(panel);
	}
}

var app = App();
app.run();
