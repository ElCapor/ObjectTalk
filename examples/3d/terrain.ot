#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2021 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class MyApp : gui.Application {
	function setup(this, screen) {
		var ambient = gui.Ambient();
		var light = gui.Light().setPosition(20, 300, 40);

		var sun = gui.Sun()
			.setElevation(0.4)
			.setAzimuth(pi / 2.0);

		var sky = gui.Sky(sun);

		var fog = gui.Fog()
			.setColor("#a0a0a0")
			.setDistances(600, 2000);

		var water = gui.Water()
			.setSize(4000)
			.setNormalMap(gui.Texture(__DIR__ + "/assets/waternormals.jpg"));

		var noisemap = gui.NoiseMap()
			.setScaleZ(200.0)
			.setOffsetZ(-100.0);

		var terrain = gui.Terrain()
			.setNoiseMap(noisemap)
			.setTextureScale(50)
			.setRegionTransitions(1.0, 15.0, 25.0, 5.0)
			.setRegion1Texture(gui.Texture(__DIR__ + "/assets/sand.jpg"))
			.setRegion2Texture(gui.Texture(__DIR__ + "/assets/terrain.jpg"))
			.setRegion3Texture(gui.Texture(__DIR__ + "/assets/rock.jpg"))
			.setRegion4Texture(gui.Texture(__DIR__ + "/assets/snow.jpg"));

		var scene = gui.Scene()
			.add(sun)
			.add(sky)
			.add(fog)
			.add(water)
			.add(terrain);

		var camera = gui.Camera()
			.setMouseControl(true)
			.setPitchLimits(0.01, pi / 2.0 - 0.01)
			.setPitch(0.1)
			.setDistanceLimits(20.0, 1000.0)
			.setDistance(400.0)
			.setAngle(-pi / 2.0)
			.setClipping(0.1, 4000);

		var view = gui.View()
			.setScreenArea(25, 0, 75, 100)
			.setCamera(camera)
			.setScene(scene);

		var panel = gui.Panel(0, 0, 25, 100);

		var node = gui.TreeNode("Sun:");
		node.add(gui.SunController(sun));
		panel.add(node);

		node = gui.TreeNode("Sky:");
		node.add(gui.SkyController(sky));
		panel.add(node);

		node = gui.TreeNode("Fog:");
		node.add(gui.FogController(fog));
		panel.add(node);

		node = gui.TreeNode("Water:");
		node.add(gui.WaterController(water));
		panel.add(node);

		node = gui.TreeNode("Noise Map:");
		node.add(gui.NoiseMapController(noisemap));
		panel.add(node);

		node = gui.TreeNode("Terrain:");
		node.add(gui.TerrainController(terrain));
		panel.add(node);

		screen.add(view).add(panel);
	}
}

var app = MyApp();
app.setAntiAliasing(2).run("3D Terrain");
