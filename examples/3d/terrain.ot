#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2021 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class MyApp : gui.Application {
		function setup(this, screen) {
		var ambient = gui.Ambient();
		var light = gui.Light().setPosition(10, 20, 30);

		var fog = gui.Fog()
			.setColor("#79838e")
			.setDistances(80, 250);

		var skybox = gui.Skybox(__DIR__ + "/assets/sky.ktx");
		var heightmap = gui.HeightMap(__DIR__ + "/assets/heightmap.png", 10);

		var terrain = gui.Mesh()
			.setGeometry(gui.PlaneGeometry()
				.setWidth(200)
				.setHeight(200)
				.setWidthSegments(200)
				.setHeightSegments(200)
				.setHeightMap(heightmap))
			.setMaterial(gui.Material()
				.setBlendMap(gui.BlendMap(
					gui.Texture(__DIR__ + "/assets/blendmap.png"),
					gui.Texture(__DIR__ + "/assets/grass.png"),
					gui.Texture(__DIR__ + "/assets/mud.png"),
					gui.Texture(__DIR__ + "/assets/grassflowers.png"),
					gui.Texture(__DIR__ + "/assets/path.png"))));

		var trees = gui.Mesh()
			.setGeometry(gui.ModelGeometry(__DIR__ + "/assets/pine.obj"))
			.setMaterial(gui.Material()
				.setTexture(gui.Texture(__DIR__ + "/assets/pine.png")))
			.setHoles(true);

		for c in range(200) {
			var x = 1.0.random();
			var z = 1.0.random();
			var y = heightmap.getHeight(x, z);
			x = -90.0 + 180.0 * x;
			z = -90.0 + 180.0 * z;

			trees.addInstance(gui.Matrix()
				.translate(x, y, z)
				.scale(0.5, 0.5, 0.5));
		}

		var scene = gui.Scene()
			.add(ambient)
			.add(light)
			.add(fog)
			.add(skybox)
			.add(terrain)
			.add(trees);

		var camera = gui.Camera()
			.setMouseControl(true)
			.setPitchLimits(0.1, pi / 2.0 - 0.1)
			.setPitch(0.1)
			.setDistanceLimits(20.0, 300.0)
			.setDistance(100.0)
			.setFOV(45)
			.setClipping(0.1, 1000);

		var view = gui.View()
			.setCamera(camera)
			.setScene(scene);

		screen.add(view);
	}
}

var app = MyApp();
app.run("3D Terrain");
