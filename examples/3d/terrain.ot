#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2022 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class App : gui.App {
	function setup(this) {
		var sun = gui.Sun()
			.setElevation(0.4)
			.setAzimuth(pi / 2.0)
			.castShadow(1500, 2000, 500, 3000, false);

		var sky = gui.Sky()
			.setSun(sun)
			.setRadius(100);

		var fog = gui.Fog()
			.setColor("#a0a0a0")
			.setLimits(100, 2000, 2000, 5000)
			.setDistances(1000, 3000)
			.setSun(sun);

		var water = gui.Water()
			.setSize(4000)
			.receiveShadow(true)
			.setNormalMap(gui.Texture(__DIR__ + "/assets/waternormals.jpg"));

		var terrainmap = gui.TerrainMap()
			.setOctaves(6)
			.setScaleZ(200.0)
			.setOffsetZ(-100.0);

		var terrain = gui.Terrain()
			.castShadow(true)
			.receiveShadow(true)
			.setTerrainMap(terrainmap)
			.setViewingDistance(3000)
			.setTextureScale(100)
			.setRegionTransitions(0, 2, 5, 5, 20, 7)
			.setRegion1Texture(gui.Texture(__DIR__ + "/assets/sand.jpg"))
			.setRegion2Texture(gui.Texture(__DIR__ + "/assets/terrain.jpg"))
			.setRegion3Texture(gui.Texture(__DIR__ + "/assets/rock.jpg"))
			.setRegion4Texture(gui.Texture(__DIR__ + "/assets/snow.jpg"));

		var scene = gui.Scene()
			.add(sun)
			.add(sky)
			.add(fog)
			.add(water)
			.add(terrain);

		var camera = gui.Camera()
			.setPerspective(45, 20, 4000)
			.setNearFarLimits(10, 35, 1000, 5000)
			.setFirstPersonMode()
			.setPosition(0, 75, 0)
			.setPitchLimits(-0.4, 0.4)
			.setHeightLimits(50, 200);

		var view = gui.View()
			.setScreenArea(25, 0, 75, 100)
			.setCamera(camera)
			.setScene(scene);

		var panel = gui.Panel(0, 0, 25, 100);

		var node = gui.TreeNode("Camera:", false);
		node.add(gui.CameraController(camera));
		panel.add(node);

		node = gui.TreeNode("Sun:");
		node.add(gui.SunController(sun));
		panel.add(node);

		node = gui.TreeNode("Sky:");
		node.add(gui.SkyController(sky));
		panel.add(node);

		node = gui.TreeNode("Fog:", false);
		node.add(gui.FogController(fog));
		panel.add(node);

		node = gui.TreeNode("Water:");
		node.add(gui.WaterController(water));
		panel.add(node);

		node = gui.TreeNode("Terrain Map:");
		node.add(gui.TerrainMapController(terrainmap));
		panel.add(node);

		node = gui.TreeNode("Terrain:");
		node.add(gui.TerrainController(terrain));
		panel.add(node);

		this.add(view).add(panel);
	}
}

var app = App();
os.runGUI();
