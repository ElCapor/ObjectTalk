#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2022 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class App : gui.App {
	function setup(this) {
		var ambient = gui.Ambient();
		var light = gui.Light().setPosition(1, 2, 4);

		var floor = gui.Mesh()
			.setGeometry(gui.PlaneGeometry(10, 10))
			.setMaterial(gui.Material("color", "white"))
			.translate(0.0, -3.0, 0.0)
			.rotateX(-pi / 2.0);

		var object = gui.Mesh()
			.setGeometry(gui.TorusGeometry())
			.setMaterial(gui.Material("material", "gold"));

		this.addAnimation(gui.Animation()
			.from(0.0)
			.to(pi * 2.0)
			.during(15000)
			.continuous()
			.onStep(function(angle) {
				object.rotateAroundVector(angle, 1, 1, 0);
			}));

		var scene = gui.Scene()
//			.setShadow(true)
			.add(ambient)
			.add(light)
			.add(floor)
			.add(object);

		var camera = gui.Camera()
			.setPerspective(45, 0.1, 100)
			.setPosition(0, 8, 10);

		var view = gui.View()
			.setScreenArea(0, 0, 100, 100)
			.setCamera(camera)
			.setScene(scene);

		this.add(view);
	}
}

var app = App();
os.runGUI();
