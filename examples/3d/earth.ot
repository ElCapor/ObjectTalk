#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2022 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

class App : gui.App {
	function setup(this) {
		var ambient = gui.AmbientLight();
		var light = gui.DirectionalLight(-1, -4, -4);

		var fog = gui.Fog()
			.setColor("black")
			.setDistances(4, 5.5);

		var skybox = gui.Skybox(
			Path(__DIR__, "assets", "space", "right.jpg"),
			Path(__DIR__, "assets", "space", "left.jpg"),
			Path(__DIR__, "assets", "space", "top.jpg"),
			Path(__DIR__, "assets", "space", "bottom.jpg"),
			Path(__DIR__, "assets", "space", "front.jpg"),
			Path(__DIR__, "assets", "space", "back.jpg"));

		var grid = gui.Mesh()
			.setGeometry(gui.SphereGeometry()
				.setRadius(0.99)
				.setWidthSegments(60)
				.setHeightSegments(30))
			.setMaterial(gui.ColoredMaterial("#505050"))
			.setWireframe(true);

		var surface = gui.Mesh()
			.setGeometry(gui.SphereGeometry()
				.setWidthSegments(64)
				.setHeightSegments(32))
			.setMaterial(gui.TexturedMaterial()
				.setTexture(Path(__DIR__, "assets", "earth.png"))
				.setDoubleSided());

		var clouds = gui.Mesh()
			.setGeometry(gui.SphereGeometry()
				.setRadius(1.01)
				.setWidthSegments(64)
				.setHeightSegments(32))
			.setMaterial(gui.TexturedMaterial()
				.setTexture(Path(__DIR__, "assets", "clouds.png"))
				.setDoubleSided());

		var earth = gui.Group()
			.add(grid)
			.add(surface)
			.add(clouds);

		var scene = gui.Scene()
			.add(ambient)
			.add(light)
			.add(fog)
			.add(skybox)
			.add(earth);

		var camera = gui.Camera()
			.setCircleTargetMode()
			.setDistance(3.0)
			.setDistanceLimits(2.2, 5.0);

		var view = gui.View()
			.setCamera(camera)
			.setScene(scene);

		this.add(view);
	}
}

var app = App();
os.runGUI();
