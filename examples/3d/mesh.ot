#!/usr/local/bin/ot

//	ObjectTalk Scripting Language
//	Copyright (c) 2020-2022 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

var gui = import("gui");

// specify default values
var defaultGeometry = "box";
var defaultMaterial = "gold";
var defaultRenderer = "textured";

// create combobox value lists
var geometries = [
	"box", "circle", "cylinder", "plane", "sphere",
	"torus", "bunny", "column", "teapot"
];

var textures = {
	"earth" : Path(__DIR__, "assets", "earth.png"),
	"moon"  : Path(__DIR__, "assets", "moon.png"),
	"grass" : Path(__DIR__, "assets", "grass.jpg"),
	"uvmap" : Path(__DIR__, "assets", "uv.jpg")
};

var materials = gui.getDefaultMaterialNames().merge(textures.keys());

var renderers = [
	"textured", "wireframe"
];

// define our application
class App : gui.App {
	function setup(this) {
		// light and fog
		this.ambient = gui.AmbientLight();

		this.light = gui.DirectionalLight()
			.setDirection(-25, -25, -40)
			.setColor("#c0c0c0");

		this.fog = gui.Fog()
			.setColor("#808080")
			.setDistances(0, 100)
			.disable();

		// set default geometry and material
		this.setGeometry(defaultGeometry);
		this.setMaterial(defaultMaterial);

		// create all components
		this.camera = gui.Camera()
			.setPerspective(60, 0.1, 100)
			.setPosition(0, 40, 40)
			.setTarget(0, -5, -10);

		this.grid = gui.Mesh()
			.setGeometry(gui.PlaneGeometry(50, 50, 10, 10))
			.setMaterial(gui.FixedMaterial("white"))
			.setWireframe(true)
			.rotateX(-pi / 2.0);

		this.mesh = gui.Mesh()
			.setGeometry(this.geometry)
			.setMaterial(this.material);

		this.addAnimation(gui.Animation()
			.from(0.0)
			.to(pi * 2.0)
			.during(20000)
			.continuous()
			.onStep(function(angle) {
				this.mesh.rotateY(-angle);
			}));

		// create scene
		var scene = gui.Scene()
			.add(this.ambient)
			.add(this.light)
			.add(this.fog)
			.add(this.grid)
			.add(this.mesh);

		// create view
		this.add(gui.View()
			.setScreenArea(20, 0, 80, 100)
			.setCamera(this.camera)
			.setScene(scene));

		// enable default renderer
		if (defaultRenderer != "textured") {
			this.mesh.setWireframe(true);
		}

		// create control panel
		this.add(this.createPanel());
	}

	function createPanel(this) {
		var panel = gui.Panel(0, 0, 20, 100);

		var node = gui.TreeNode("Camera:");
		node.add(gui.CameraController(this.camera));
		panel.add(node);

		node = gui.TreeNode("Ambient Light:");
		node.add(gui.AmbientLightController(this.ambient));
		panel.add(node);

		node = gui.TreeNode("Directional Light:");
		node.add(gui.DirectionalLightController(this.light));
		panel.add(node);

		node = gui.TreeNode("Fog:");
		node.add(gui.FogController(this.fog));
		panel.add(node);

		node = panel.add(gui.TreeNode("3D Object:"));

		node.add(gui.Combobox("Geometry", geometries, defaultGeometry, function(selected) {
			this.setGeometry(selected);
		}));

		node.add(gui.Combobox("Material", materials, defaultMaterial, function(selected) {
			this.setMaterial(selected);
		}));

		node.add(gui.Combobox("Renderer", renderers, defaultRenderer, function(selected) {
			this.setRenderer(selected);
		}));

		node.add(gui.Checkbox("Show grid", true, function(checked) {
			this.grid.setEnabled(checked);
		}));

		return panel;
	}

	function setGeometry(this, name) {
		if (name == "box") {
			this.geometry = gui.BoxGeometry(30, 30, 30);

		} elif (name == "circle") {
			this.geometry = gui.CircleGeometry(20, 30);

		} elif (name == "cylinder") {
			this.geometry = gui.CylinderGeometry(16, 16, 40, 64);

		} elif (name == "plane") {
			this.geometry = gui.PlaneGeometry(40, 40);

		} elif (name == "sphere") {
			this.geometry = gui.SphereGeometry(21, 64, 64);

		} elif (name == "torus") {
			this.geometry = gui.TorusGeometry(15, 5, 32, 32);

		} elif (name == "bunny") {
			this.geometry = gui.ModelGeometry(Path(__DIR__, "assets", "bunny.obj"), 16);

		} elif (name == "column") {
			this.geometry = gui.ModelGeometry(Path(__DIR__, "assets", "column.obj"), 2);

		} elif (name == "teapot") {
			this.geometry = gui.ModelGeometry(Path(__DIR__, "assets", "teapot.obj"), 28);
		}

		if (this.has("mesh")) {
 			this.mesh.setGeometry(this.geometry);
		}
	}

	function setMaterial(this, name) {
		if (name in gui.getDefaultMaterialNames()) {
			this.material = gui.ColoredMaterial(name)
				.setDoubleSided();

		} else {
			this.material = gui.TexturedMaterial(textures[name])
				.setDoubleSided();
		}

		if (this.has("mesh")) {
			this.mesh.setMaterial(this.material);
		}
	}

	function setRenderer(this, name) {
		this.mesh.setWireframe(name == "wireframe");
	}
}

// instantiate application and run it
var app = App();
os.runGUI();
